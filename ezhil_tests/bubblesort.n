# (C) முத்தையா அண்ணாமலை 2013
# (A) என். சொக்கன்
# எழில் தமிழ் நிரலாக்க மொழி உதாரணம்

## Bubble Sort Example
## எண்களை Bubble Sort முறைப்படி அடுக்கும் உதாரணம்

## இது நிரல் ஒரு பட்டியலில் உள்ள எண்களை Bubble Sort என்ற முறைப்படி ஏறுவரிசையிலும் பின்னர் அதையே இறங்குவரிசையிலும் அடுக்கித் தரும்

நிரல்பாகம் மாற்றிப்போடு(ஒருபட்டியல், நிலை1, நிலை2)

  ## இது நிரல்பாகம் தரப்பட்ட பட்டியலில் உள்ள இரு நிலைகளை மாற்றி(swap)த்தரும். உதாரணமாக, 8, 9 என்ற எண்கள் தரப்பட்டால், 8ம் இடத்தில் உள்ள எண் 9ம் இடத்துக்கும், 9ம் இடத்தில் உள்ள எண் 10ம் இடத்துக்கும் மாறும்

  எண்1 = எடு(ஒருபட்டியல், நிலை1)
  எண்2 = எடு(ஒருபட்டியல், நிலை2)

        வெளியேஎடு(ஒருபட்டியல், நிலை1)
        நுழைக்க(ஒருபட்டியல், நிலை1, எண்2)
        வெளியேஎடு(ஒருபட்டியல், நிலை2)
        நுழைக்க(ஒருபட்டியல், நிலை2, எண்1)
  
  பின்கொடு ஒருபட்டியல்

முடி

நிரல்பாகம் வரிசை(ஒருபட்டியல், வரிசை)

  ## வரிசை = 1 என்றால் ஏறுவரிசை, வரிசை = 2 என்றால் இறங்குவரிசை

  நீளம் = len(ஒருபட்டியல்)
  குறைநீளம் = நீளம் - 1

  @(குறைநீளம் != -1) வரை
    மாற்றம் = -1
    @(எண் = 0, எண் < குறைநீளம், எண் = எண் + 1) ஆக
      முதலெண் = எடு(ஒருபட்டியல், எண்)
      இரண்டாமெண் = எடு(ஒருபட்டியல், எண் + 1)
      @(வரிசை == 1) ஆனால்
        @(முதலெண் > இரண்டாமெண்) ஆனால்

          ## சிறிய எண்ணை முன்னுக்குத் தள்ளவேண்டும்

          மாற்றிப்போடு(ஒருபட்டியல், எண், எண் + 1)
          மாற்றம் = எண்
        முடி
      முடி
      @(வரிசை == 2) ஆனால்
        @(இரண்டாமெண் > முதலெண்) ஆனால்

          ## பெரிய எண்ணை முன்னுக்குத் தள்ளவேண்டும்

          மாற்றிப்போடு(ஒருபட்டியல், எண், எண் + 1)
          மாற்றம் = எண்
        முடி
      முடி
    முடி
    குறைநீளம் = மாற்றம்
  முடி
  பின்கொடு ஒருபட்டியல்
முடி

## மாதிரிக்கு நாம் ஏழு எண்களை எடுத்துக்கொள்வோம்

எண்கள் = [5, 1, 10, 8, 1, 21, 4, 2]
எண்கள்பிரதி = எண்கள்

பதிப்பி "ஆரம்பப் பட்டியல்:"
பதிப்பி எண்கள்

ஏறும்பட்டியல் = வரிசை(எண்கள், 1)

பதிப்பி "ஏறு வரிசையில் அமைக்கப்பட்ட பட்டியல்:"
பதிப்பி ஏறும்பட்டியல்

இறங்கும்பட்டியல் = வரிசை(எண்கள், 2)

பதிப்பி "இறங்கு வரிசையில் அமைக்கப்பட்ட பட்டியல்:"
பதிப்பி இறங்கும்பட்டியல்
